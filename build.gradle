/*
 * Template microservice.
 */

buildscript {
  repositories {
    if(version == "DEV") {
      mavenLocal()
    }
    jcenter()
    mavenCentral()
  }
}

plugins {
  id 'java'
  id 'com.inomial.grow-plugin' version '1.2.3'
  id "com.github.ben-manes.versions" version "0.33.0"
  id 'io.spring.dependency-management' version '1.0.1.RELEASE'
}

apply plugin: "application"
apply plugin: "com.inomial.grow-plugin"

// description = "change me to a brief description of what this application does. this is viewable in `gradle -q projects` and is also set in the container image labels."

if (description == null && project.name != 'secore-template') {
  throw new GradleException('description not set in build.gradle')
}

// let Jenkins choose the version
version = file('build.version').text.trim()

mainClassName = "com.inomial.${project.name.replaceAll('-', '.')}.Main"

// We don't need these for Docker.
tasks.distTar.enabled = false
tasks.distZip.enabled = false

tasks.withType(JavaCompile) {
  options.compilerArgs << '-parameters'
}

// runtime logging dependencies
dependencyManagement {
  imports {
    mavenBom 'org.apache.logging.log4j:log4j-bom:2.13.3'
  }
}

dependencies {
  // use `./gradlew dependencyUpdates` to check for updates to these dependencies

  // Use this API to log
  implementation 'org.slf4j:slf4j-api:1.7.30'

  // Supply log4j2 logging implementation, plus slf4j bindings.
  implementation 'com.telflow.logging:telflow-logging:10.10.10'
  implementation 'org.apache.logging.log4j:log4j-api'
  implementation 'org.apache.logging.log4j:log4j-core'
  // ensure 3rd party libs using other logging APIs are forwarded to slf4j:
  implementation 'org.slf4j:jul-to-slf4j:1.7.30'
  implementation 'org.apache.logging.log4j:log4j-1.2-api'
  implementation 'org.apache.logging.log4j:log4j-slf4j-impl'
  implementation 'org.slf4j:jcl-over-slf4j:1.7.30'

  implementation 'com.inomial:secore:3.0.6'

  // Include this, for example, if you want to use grow (also uncomment: dependsOn 'growJar')
  // runtimeOnly 'com.inomial:sql-core:1.1.+';

  // Include this if you're working with messages
  // compile 'com.inomial:cim:1.0.+'

  // Delete this if you do not need Consul support:
  implementation 'com.telflow.factory:configuration-management:1.0.0'

  // healthcheck server:
  implementation 'com.telflow.assembly:healthcheck:10.10.10'
  implementation 'org.eclipse.jetty:jetty-server:9.4.29.v20200521'
  implementation 'org.eclipse.jetty:jetty-webapp:9.4.29.v20200521'

  testImplementation 'junit:junit:4.12'
}

repositories {
  maven {
    url "https://maven.inomial.com/"
    content {
      includeGroupByRegex "com\\.inomial.*"
    }
  }
  maven {
    url "https://nexus.office.dgit.biz:8443/nexus/content/repositories/dgit-maven-releases"
    mavenContent {
      releasesOnly()
    }
    content {
      includeGroupByRegex "com\\.telflow.*"
      includeGroupByRegex "com\\.dgitsystems.*"
    }
  }

  mavenCentral()

  if (version == "DEV") {
    mavenLocal {
      content {
        includeGroupByRegex "com\\.inomial.*"
        includeGroupByRegex "com\\.telflow.*"
        includeGroupByRegex "com\\.dgitsystems.*"
      }
    }
  }
}

task growJar(type: Jar, dependsOn: configurations.runtimeClasspath) {
  classifier = "grow"

  from(configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }
    .collect { zipTree(it) }) {
    include "GROW/**"
  }

  from('src/main/grow') {
    into("GROW/APPLICATION")
  }

  from("${buildDir}/tmp/dependencies.grow") {
    into("GROW/APPLICATION")
  }
}

jar {
  dependsOn 'grow'
//  Uncomment if you want to use grow
//  dependsOn 'growJar'

  manifest {
    attributes(
      "Main-Class": mainClassName,
      "Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '))
  }
}

task createProperties(dependsOn: classes) {
  doLast {
    new File("$buildDir/version.properties").withWriter { w ->
        Properties p = new Properties()
        p['version'] = project.version.toString()
        p['project'] = project.name
        p['description'] = "'" + project.description + "'"
        p.store w, null
    }
  }
}

task mkdocker(type: Exec, dependsOn: build) {
  commandLine './mkdocker'
}

assemble {
  dependsOn createProperties
}

build.finalizedBy(mkdocker)


task copyToLib( type: Copy ) {
    into "docker/lib"
    from configurations.runtimeClasspath
}

task cleanDocker( type: Delete ) {
  delete fileTree(
      dir: 'docker',
      include: ['*.jar', 'lib/**']
    )
  delete 'docker/GROW'
}

mkdocker { dependsOn cleanDocker, copyToLib }

clean.dependsOn cleanDocker

// Add sources jar
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
  // Include source archive in Inomial Maven repository so Javadoc descriptions are available.
  archives sourcesJar
}
